#Использовать fs
#Использовать 1commands

#Область ОбработчикиСобытий

Процедура ПередСборкой(Знач РабочийКаталог) Экспорт
	
	MSBuild = "C:\Program Files (x86)\Microsoft Visual Studio\2019\Community\MSBuild\Current\Bin\msbuild.exe";

	КаталогДвоичныхДанных = ОбъединитьПути(РабочийКаталог, "bin");
	ФС.ОбеспечитьПустойКаталог(КаталогДвоичныхДанных);

	ПостроитьРешение(MSBuild);

	ДвоичныеФайлы = Новый Массив();
	ДвоичныеФайлы.Добавить("CoverageBSL.AddIn.dll");
	ДвоичныеФайлы.Добавить("DebugBSL.dll");
	ДвоичныеФайлы.Добавить("DebugClientBSL.dll");
	ДвоичныеФайлы.Добавить("CoverageBSL.dll");

	КаталогСборкиДвоичныхДанных = ОбъединитьПути("src", "CoverageBSL.AddIn", "bin");
	КаталогИсточник = ОбъединитьПути(КаталогСборкиДвоичныхДанных, "Release", "net48");

	Для каждого ДвоичныйФайл Из ДвоичныеФайлы Цикл
	 	ФайлИсточник = ОбъединитьПути(КаталогИсточник, ДвоичныйФайл);
	 	ФайлНазначение = ОбъединитьПути(КаталогДвоичныхДанных, ДвоичныйФайл);
	 	КопироватьФайл(ФайлИсточник, ФайлНазначение);
	КонецЦикла;

	КопироватьФайл("build\package-loader.os", "package-loader.os");

КонецПроцедуры

Процедура ПослеСборки(РабочийКаталог, ПутьКФайлуПакета) Экспорт

	УдалитьФайлы("package-loader.os");

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПостроитьРешение(MSBuild)
	
	ФайлРешения = ОбъединитьПути("src", "CoverageBSL.sln");

	Команда = Новый Команда();
	Команда.УстановитьКоманду(MSBuild);
	Команда.ПоказыватьВыводНемедленно(Истина);
	Команда.ДобавитьПараметр(ФайлРешения);
	Команда.ДобавитьПараметр("-p:Configuration=Release");

	Команда.Исполнить();

КонецПроцедуры

Функция ВерсияБиблиотеки()

	РегулярноеВыражение = Новый РегулярноеВыражение("<ReleaseNumber .*>(.+)</ReleaseNumber>");

	ТекстовыйДокумент = Новый ТекстовыйДокумент();
	ТекстовыйДокумент.Прочитать("src/Common/project.props", КодировкаТекста.UTF8);
	СвойстваСборкиТекст = ТекстовыйДокумент.ПолучитьТекст();

	Совпадение = РегулярноеВыражение.НайтиСовпадения(СвойстваСборкиТекст)[0];
	ВерсияБиблиотеки = Совпадение.Группы[1].Значение;

	Возврат ВерсияБиблиотеки;

КонецФункции

#КонецОбласти

Описание
	.Имя("coveragebsl")
	.Версия(ВерсияБиблиотеки())
	.Описание("Плагин замера покрытия для 1С:Предприятия")
	.ВерсияСреды("1.6.0")
	.ВключитьФайл("bin")
	.ВключитьФайл("examples")
	.ВключитьФайл("package-loader.os")
	.ВключитьФайл("README.md")
;